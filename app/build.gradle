plugins {
    alias libs.plugins.androidApplication
    alias libs.plugins.kapt
    alias libs.plugins.kotlinAndroid
    alias libs.plugins.kotlinParcelize
    alias libs.plugins.kotlinSerialization
    alias libs.plugins.ksp
    alias libs.plugins.navigationSafeArgs
    alias libs.plugins.hiltAndroid
}

android {
    compileSdk 34
    buildToolsVersion = "30.0.3"
    namespace 'org.qosp.notes'
    defaultConfig {
        applicationId "io.github.quillpad"
        minSdkVersion 25
        targetSdkVersion 34
        versionCode 24
        versionName "1.5-dev"

        testInstrumentationRunner "org.qosp.notes.TestRunner"

        // export schema
        // https://stackoverflow.com/a/44645943/4594587
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    if (project.hasProperty("keystore")) {
        signingConfigs {
            release {
                def keystoreFileArg = project.property("keystore").toString()
                def storePassArg = project.property("storepass").toString()
                def keyName = project.property("keyalias").toString()
                def keypassArg = project.property("keypass").toString()
                storeFile file(keystoreFileArg)
                storePassword storePassArg
                keyAlias keyName
                keyPassword keypassArg
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            if (project.hasProperty("keystore")) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "versions"
    productFlavors {
        googleFlavor {
            dimension "versions"
            buildConfigField "boolean", "IS_GOOGLE", "true"
        }
        defaultFlavor {
            dimension "versions"
            buildConfigField "boolean", "IS_GOOGLE", "false"
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

        compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    kapt {
        javacOptions {
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }

    namespace 'org.qosp.notes'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'
    implementation 'androidx.test.ext:junit-ktx:1.1.5'
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
    coreLibraryDesugaring libs.coreLibraryDesugaring
    // AndroidX
    implementation libs.bundles.kotlin.androidX

    implementation libs.bundles.kotlin.deps

    // Material Components
    implementation libs.material

    // Room
    ksp libs.roomCompiler
    implementation libs.roomRuntime
    implementation libs.roomKtx
    testImplementation libs.roomTesting

    // Lifecycle
    implementation libs.bundles.kotlin.lifecycle

    // Security
    implementation libs.securityCrypto

    // Flow Preferences
    implementation libs.flowPreferences

    // DataStore Preferences
    implementation libs.datastorePreferences
    implementation libs.datastoreext

    // Markwon
    implementation libs.bundles.markwon

    // Work Manager
    implementation libs.workRuntimeKtx
    androidTestImplementation libs.workTesting

    // Coil
    implementation libs.coil
    implementation libs.coilVideo
    implementation libs.coilGif

    // PhotoView
    implementation libs.photoview

    // Hilt
    implementation libs.androidxHiltWork
    implementation libs.hiltAndroid
    androidTestImplementation libs.hiltAndroidTesting
    kaptAndroidTest libs.hiltAndroidCompiler
    kapt libs.hiltCompiler
    kapt libs.androidxHiltCompiler

    // ExoPlayer
    implementation libs.exoplayerCore
    implementation libs.exoplayerUi

    // Retrofit
    implementation libs.retrofit
    implementation libs.retrofit2Convertor

    // LeakCanary
    debugImplementation libs.leakcanaryAndroid
}
